import "runtime/print.tan";
import "runtime/debug.tan";

@test_comp_error(i32_to_i8) {
    var a: i8 = 32;
}

@test_comp_error(f32_to_i32) {
    var a: i32 = 8.;
}

fn test_implicit_cast() : void {
    // rule #1
    var signed: i32 = 1 as i16;
    assert(signed == 1);
    var unsigned: u32 = 2 as u16;
    assert(unsigned == 2);
    @test_comp_error(int_narrowing) {
        var a: i16 = signed;
        var b: u16 = unsigned;
    }

    // rule #2
    signed = 5 as u16;
    assert(signed == 5);
    @test_comp_error(u64_to_i32) {
        signed = 5 as u64;
    }

    // rule #3
    unsigned = 3 as i32;
    assert(unsigned == 3);
    unsigned = 4 as i16;
    assert(unsigned == 4);
    @test_comp_error(i64_to_u32) {
        unsigned = -1 as i64;
    }

    // rule #4
    var f: f32 = 1.0;
    var d: f64 = 2.0;
    d = f;
    assert(d == 1.0);
    @test_comp_error(f64_to_f32) {
        f = d;
    }

    // rule #5
    signed = -5;
    f = signed;
    d = signed;
    assert(f == -5.0);
    assert(d == -5.0);
    @test_comp_error(float_to_int) {
        signed = f;
    }

    // rule #6
    assert(400);
    assert(signed);
    assert(unsigned);
    assert(f);
    assert(d);
    var pi = &signed;
    var pf = &f;
    assert(pi);
    assert(pf);
    assert(f && signed);

    // rule #7
    signed = true;
    assert(signed);
    assert(signed == 1);
    signed = false;
    assert(!signed);
    assert(signed == 0);
    f = true;
    assert(f);
    assert(f == 1.0);
    f = false;
    assert(!f);
    assert(f == 0);
}

pub fn main() : int {
    var a1: i32 = 100;
    assert(a1 == 100);
    var a2: u32 = a1 as u32;
    assert(a2 == 100 as u32);
    assert(a2 == 100u);
    var a3: u8 = a2 as u8;
    assert(a3 == 100 as u8);
    var a4: i8 = a3 as i8;
    assert(a4 == 100 as i8);
    assert(a4 == 100.0 as i8);
    var a5: i32 = a4 as i32;
    assert(a5 == 100);
    var a6: f32 = a5 as float;
    assert(a6 == 100.0);
    var a7: f64 = a6 as f64;
    assert(a7 as f32 == 100.0);
    a7 = a5 as f64;
    assert(a7 == 100 as f64);

    // float and signed/unsigned int
    var fp: float = -1.0 as f32;
    assert(fp as int == -1);
    assert(fp as u32 == 4294967295u); // underflow
    assert(fp as bool);

    // arrays
    var array1: i32[3] = [0, 1, 2];
    var parray1: i32* = &array1[0];
    assert(*parray1 == array1[0]);

    test_implicit_cast();
    print("SUCCESS\n");
    return 0;
}
